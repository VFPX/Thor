Lparameters toUpdateInfo

Local lcNote
AddProperty(toUpdateInfo, 'AvailableVersion',	'Thor - 1.47.02 - January 13 2024- 20240113'		)
AddProperty(toUpdateInfo, 'SourceFileUrl', 		'https://raw.githubusercontent.com/VFPX/Thor/master/ThorUpdater/Thor.zip')
AddProperty(toUpdateInfo, 'LinkPrompt', 		'Thor Home Page')
AddProperty(toUpdateInfo, 'Link', 				'https://github.com/VFPX/Thor')

Text to lcNote NoShow
This update changes the Thor Check for Updates process to look for updates at GitHub.com rather than VFPXRepository.com, which is being deprecated.
EndText

AddProperty(toUpdateInfo, 'Notes', 				lcNote)

* If necessary, update Thor_Proc_Check_For_Updates.prg to point to GitHub instead of VFPXRepository.com.
* In that case, we'll stop the update process and prompt the user to do it again.

loThor        = execscript(_screen.cThorDispatcher, 'Thor Engine=')
lcProcsFolder = addbs(loThor.cToolsFolder) + 'Procs\'
lcUpdaterPRG  = lcProcsFolder + 'Thor_Proc_Check_For_Updates.prg'
lcCode        = filetostr(lcUpdaterPRG)
if 'vfpxrepository' $ lcCode
	lcCode = strtran(lcCode, ;
		'http://vfpxrepository.com/dl/thorupdate/Updater_PRGs/Updates.zip', ;
		'https://raw.githubusercontent.com/VFPX/Thor/master/ThorUpdater/Updates.zip')
	strtofile(lcCode, lcUpdaterPRG)

	lcInstallPRG = lcProcsFolder + 'Thor_Proc_DownloadAndInstallUpdates.prg'
	lcCode       = filetostr(lcInstallPRG)
	lcCode       = strtran(lcCode, ;
		[	lcUpdateInfo = ''], ;
		[	addproperty(_screen, 'cThorAppFolder', '] + loThor.cAppFolder + [')] + chr(13) + ;
		[	lcUpdateInfo = ''])
	lcCode       = strtran(lcCode, ;
		[	Endfor &] + [& lnI = 1 to Alen(laUpdates)], ;
		[	Endfor &] + [& lnI = 1 to Alen(laUpdates)] + chr(13) + chr(13) + ;
		[	if not 'loException' $ _screen.cThorDispatcher] + chr(13) + ;
		[		_screen.cThorDispatcher = strtran(_screen.cThorDispatcher, ] + ;
		["_Screen.AddProperty('oThor', Newobject ('oThor', 'Thor.vcx', lcThorApp, lcThorFolder))", ] + ;
		["try" + chr(13) + "_Screen.AddProperty('oThor', Newobject ('oThor', 'Thor.vcx', lcThorApp, lcThorFolder))" + ] + ;
		[chr(13) + "catch to loException" + chr(13) + "endtry")] + chr(13) + ;
		[	endif] + chr(13) + ;
		[	if lower(loUpdate.AppName) = 'thor.app'] + chr(13) + ;
		[		erase (addbs(_screen.cThorAppFolder) + 'Thor.app')] + chr(13) + ;
		[	endif])
	strtofile(lcCode, lcInstallPRG)

	messagebox('The Check for Updates function has itself been updated. VFP will exit. ' + ;
		'Please run the Check for Updates function again to check for other updates.', ;
		48, 'Thor Updater')
	quit
endif 'vfpxrepository' $ lcCode

Execscript (_Screen.cThorDispatcher, 'Result=', toUpdateInfo)
Return toUpdateInfo
